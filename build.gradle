apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'signing'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    // Determined from apereo/cas:gradle.properties
    compile 'org.projectlombok:lombok:1.18.16'
    compile 'org.json:json:20160810'
    compile "org.apereo.cas:cas-server-core-authentication:${project.'cas.version'}"
    compile "org.apereo.cas:cas-server-core-webflow:${project.'cas.version'}"
    compile "org.apereo.cas:cas-server-core-webflow-mfa:${project.'cas.version'}"
    compile "org.apereo.cas:cas-server-support-trusted-mfa:${project.'cas.version'}"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    useGpgCmd()
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: mavenUsername, password: mavenPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: mavenUsername, password: mavenPassword)
      }

      pom.project {
        name 'ESUP-OTP-CAS'
        packaging 'jar'
        description 'Apereo CAS module of ESUP-Portailusing esup-otp-api - MFA Provider'
        url 'https://www.esup-portail.org/wiki/display/esupotp'
        licenses {
           license {
             name 'The MIT License (MIT)'
             url 'http://opensource.org/licenses/MIT'
           }
        }
        scm {
            url 'https://github.com/EsupPortail/esup-otp-cas'
        }
        developers {
           developer {
             id 'floriannari'
             name 'Nari Florian'
           }
           developer {
             id 'prigaux'
             name 'Pascal Rigaux'
           }
           developer {
             id 'vbonamy'
             name 'Vincent Bonamy'
           }                    
       }
    }
  }
 }
}

