plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'
    implementation 'org.json:json:20160810'
    implementation "org.apereo.cas:cas-server-core-util-api:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-core-authentication-api:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-core-authentication-mfa-api:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-core-web-api:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-core-webflow-api:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-core-webflow-mfa-api:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-trusted-mfa:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-trusted-mfa-core:${project.'cas.version'}"
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'ESUP-OTP-CAS'
                description = 'Apereo CAS module of ESUP-Portail using esup-otp-api - MFA Provider'
                url = 'https://www.esup-portail.org/wiki/display/esupotp'
                 licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                scm {
                    url = 'https://github.com/EsupPortail/esup-otp-cas'
                }
                developers {
                    developer {
                        id = 'floriannari'
                        name = 'Nari Florian'
                    }
                    developer {
                        id = 'prigaux'
                        name = 'Pascal Rigaux'
                    }
                    developer {
                        id = 'vbonamy'
                        name = 'Vincent Bonamy'
                    }                    
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
            authentication {
              basic(BasicAuthentication)
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.maven
}
